# Maintainer : bartus <arch-user-repoá˜“bartus.33mail.com>

pkgname=blender2.8-full-git
pkgver=2.8.r83217.bfb9680e9e5
pkgrel=1
pkgdesc="Development version of Blender 2.8 branch"
arch=(i686 x86_64)
url="http://blender.org/"
depends=(alembic libgl python python-numpy openjpeg 
         ffmpeg fftw openal freetype2 libxi openimageio opencolorio
         openvdb opencollada opensubdiv openshadinglanguage libtiff libpng
         libspnav jack libsndfile cuda)
makedepends=(git cmake boost mesa llvm)
provides=(blender-2.8)
conflicts=(blender-2.8)
license=(GPL)
source=("git://git.blender.org/blender.git#branch=blender2.8"
        "git://git.blender.org/blender-addons.git"
        "git://git.blender.org/blender-addons-contrib.git"
        "git://git.blender.org/blender-translations.git"
        "git://git.blender.org/blender-dev-tools.git"
        blender-2.8.desktop
        SelectCudaComputeArch.patch
        ffmpeg.patch
        )
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'cd108dca1c77607c6a7cc45aa284ea97'
         '9454ff7e994f72ead5027356e227cbd2'
         '9d4bfb5b3dd33e95b13cc6c7d9d2d2e1')

pkgver() {
  cd "$srcdir/blender"
  printf "2.8.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/blender"

  git submodule init
  git config submodule."release/scripts/addons".url ${srcdir}/blender-addons
  git config submodule."release/scripts/addons_contrib".url ${srcdir}/blender-addons-contrib
  git config submodule."release/datafiles/locale".url ${srcdir}/blender-translations
  git config submodule."source/tools".url ${srcdir}/blender-dev-tools
  git submodule update

  patch -Np1 -i "$srcdir"/SelectCudaComputeArch.patch
  patch -Np1 -i "$srcdir"/ffmpeg.patch

  mkdir -p "$srcdir/blender-build"
}

build() {
  cd "$srcdir/blender-build"

  export CFLAGS="${CFLAGS} -DOPENVDB_3_ABI_COMPATIBLE"
  export CXXFLAGS="${CXXFLAGS} -DOPENVDB_3_ABI_COMPATIBLE"

  cmake "$srcdir/blender" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_INSTALL_PORTABLE=OFF \
        -DWITH_ALEMBIC=ON \
        -DWITH_OPENCOLORIO=ON \
        -DWITH_FFTW3=ON \
        -DWITH_SYSTEM_GLEW=ON \
        -DWITH_CODEC_FFMPEG=ON \
        -DWITH_CODEC_SNDFILE=ON \
        -DWITH_JACK=ON \
        -DWITH_JACK_DYNLOAD=ON \
        -DWITH_SDL=ON \
        -DWITH_SDL_DYNLOAD=ON \
        -DWITH_PYTHON_INSTALL=OFF \
        -DPYTHON_VERSION=3.7 \
        -DWITH_MOD_OCEANSIM=ON \
        -DWITH_CYCLES_OPENSUBDIV=ON \
        -DWITH_CYCLES_OSL=ON \
        -DWITH_LLVM=ON \
        -DWITH_IMAGE_OPENEXR=ON \
        -DWITH_OPENSUBDIV=ON \
        -DWITH_OPENVDB=ON \
        -DWITH_OPENVDB_BLOSC=ON \
        -DWITH_OPENCOLLADA=ON \
        -DWITH_CYCLES_CUDA_BINARIES=ON \
        -DWITH_CYCLES_PTEX=OFF \
        -DCYCLES_CUDA_BINARIES_ARCH="sm_30;sm_35;sm_37;sm_50;sm_52;sm_60;sm_61;sm_62;sm_70;sm_72;sm_75"
  make

#        -DWITH_GLEW_ES=ON \
#        -DWITH_GL_EGL=ON \
#        -DWITH_GL_PROFILE_ES20=ON \
}

package() {
  cd "$srcdir/blender-build"
  make DESTDIR="$pkgdir" install
  
#  msg "add -2.8 sufix to desktop shortcut"
  sed -i 's/=blender/=blender-2.8/g' ${pkgdir}/usr/share/applications/blender.desktop
  sed -i 's/=Blender/=Blender-2.8/g' ${pkgdir}/usr/share/applications/blender.desktop
  mv ${pkgdir}/usr/share/applications/blender.desktop ${pkgdir}/usr/share/applications/blender-2.8.desktop

#  msg "add -2.8 sufix to binaries"
  mv ${pkgdir}/usr/bin/blender ${pkgdir}/usr/bin/blender-2.8
  mv ${pkgdir}/usr/bin/blender-thumbnailer.py ${pkgdir}/usr/bin/blender-2.8-thumbnailer.py
#  mv ${pkgdir}/usr/bin/blenderplayer ${pkgdir}/usr/bin/blenderplayer-2.8

#  msg "mv doc/blender to doc/blender-2.8"
  mv ${pkgdir}/usr/share/doc/blender ${pkgdir}/usr/share/doc/blender-2.8

#  msg "add -2.8 sufix to all icons"  
  for icon in `find ${pkgdir}/usr/share/icons -type f`
  do
    # ${filename##/*.} extra extenssion from path
    # ${filename%.*} extract filename form path
    # look at bash "manipulatin string" 
    mv $icon ${icon%.*}-2.8.${icon##/*.}
  done

  if [ -e "$pkgdir"/usr/share/blender/*/scripts/addons/cycles/lib/ ] ; then
    # make sure the cuda kernels are not stripped
    chmod 444 "$pkgdir"/usr/share/blender/*/scripts/addons/cycles/lib/*
  fi
}
