# Maintainer: 

pkgname=fontfinder-git
pkgver=1.4.1.r0.g9cc17b4
pkgrel=1
pkgdesc="A Google font browser for your GTK desktop, written in Rust"
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="https://gitlab.com/mmstick/fontfinder/"
license=(MIT)
depends=(gtk3 webkit2gtk)
makedepends=(git rust)
provides=(fontfinder)
conflicts=(fontfinder)
source=("git+${url}"
        $pkgname-LICENSE::https://gitlab.com/mmstick/fontfinder/raw/master/LICENSE)
sha256sums=('SKIP'
            '94fac0507f1027bc7b9ab32e34b29eb3873f587d4162de986d5310b7e8cd116b')

pkgver() {
  cd "$srcdir/${pkgname/-git/}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/${pkgname/-git/}"
  make
}

package() {
  cd "$srcdir/${pkgname/-git/}"

  make DESTDIR="${pkgdir}" install
  install -D $srcdir/$pkgname-LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
