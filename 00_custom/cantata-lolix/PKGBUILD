# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Mcder3 <mcder3[at]gmail[dot]com>
# Contributor: MisterFred <mister.fred[at]free[dot]fr>

pkgname=cantata-lolix
_pkgname=cantata
pkgver=2.3.1
pkgrel=1
pkgdesc='Qt5 client for the music player daemon (MPD), disabled replay-gain and mpd http stream'
arch=(i686 x86_64)
url='https://github.com/CDrummond/cantata'
license=(GPL)
depends=(qt5-svg libmtp libmusicbrainz5 taglib-extras media-player-info libcdio-paranoia udisks2)
optdepends=('perl-uri: dynamic playlist' 'mpd: playback')
makedepends=(cmake qt5-tools ffmpeg)
provides=(cantata)
conflicts=(cantata)
source=("https://github.com/CDrummond/cantata/releases/download/v$pkgver/${_pkgname}-$pkgver.tar.bz2")
md5sums=('d56ab9f0cebd944c592273022adad80e')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname}-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_LIBVLC=OFF \
    -DENABLE_FFMPEG=OFF \
    -DENABLE_MPG123=OFF \
    -DENABLE_HTTP_STREAM_PLAYBACK=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
