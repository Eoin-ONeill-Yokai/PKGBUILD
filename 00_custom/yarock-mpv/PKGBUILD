# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Ko≈°ava <kosava at archlinux dot us>

pkgname=yarock-mpv
pkgver=1.2.0
pkgrel=1
pkgdesc="Qt Modern Music Player with collection browse based on cover art"
arch=('i686' 'x86_64')
url="https://launchpad.net/yarock"
license=('GPL3')
depends=('htmlcxx' 'qt5-x11extras' 'taglib>=1.10' 'mpv')
makedepends=('cmake' 'qt5-tools')
provides=('yarock')
conflicts=('yarock' 'yarock-qt5')
source=("https://launchpad.net/yarock/1.x/${pkgver}/+download/Yarock_${pkgver}_Sources.tar.gz")
md5sums=('b705564249bbd277e4dba34c731a33bc')

prepare() {
  rm -rf "build"
  mkdir "build"
}

build() {
  cd build
  cmake "../Yarock_${pkgver}_Sources" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_QT5=1 \
    -DENABLE_MPV=ON \
    -DENABLE_VLC=OFF \
    -DENABLE_PHONON=OFF \
    -DTAGLIB_MIN_VERSION=1.10
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
