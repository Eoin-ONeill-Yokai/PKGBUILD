# Maintainer: Eden Rose - Contact through aur.
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG

pkgname=freeablo-git
_pkgname=freeablo
pkgver=v0.3.r704.g978ffa59
pkgrel=1
pkgdesc='Modern, FLOSS reimplementation of the Diablo 1 game engine. GIT-Version *(Engine and Game Data Installer)*'
arch=('i686' 'x86_64')
url="http://freeablo.org/"
license=('GPL3')
depends=('enet' 'librocket-git' 'libgl' 'qt5-base' 'sndio')
makedepends=('cmake' 'boost' 'git')
install=$_pkgname.install
source=("$_pkgname::git+https://github.com/wheybags/freeablo.git"
        'freeablo.sh'
        'freeablo.desktop'
        'freeablo_icon.svg')
        #'diablo-files.tar.xz::https://www.dropbox.com/s/ljshg2kwdxly66s/Diablo_FreeAblo_files.tar.xz?dl=0'
sha256sums=('SKIP'
            '96d7434be63aa095dc0187e3b3ec9f1c9a9169118905a65dd562f8a888a16dbb'
            '95ea4757e1f373e54cf899d6bef6efcf28798e6eca30d904e3710963cdd856df'
            'abdd045f931a51ddbac3ac2f5bed6564e76865dd5f57869f65bfa06779bb3869')

pkgver() {
  cd $srcdir/$_pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $srcdir
  rm -rf build
  mkdir build
  
  #if [ -e $srcdir/$pkgname ]; then
  #	ln -s $pkgname $_pkgname
  #fi

  cd $srcdir/$_pkgname
  git submodule update --init
}

build() {
  cd $srcdir/build
  #ln -s $srcdir/$pkgname/resources
   
  cmake $srcdir/$_pkgname
  make
}

package() {
  # binaries
  install -Dm755 $srcdir/build/freeablo "$pkgdir"/usr/bin/"$_pkgname"_game
  for _f in celview exedump mpqtool launcher; do
    install -Dm755 $srcdir/build/$_f "$pkgdir"/usr/bin/"$_pkgname"-$_f
  done
  # data
  install -d $pkgdir/usr/share/$_pkgname/resources
  cp -r $srcdir/$_pkgname/resources/* $pkgdir/usr/share/$_pkgname/resources/
  # doc
  install -Dm644 $srcdir/$_pkgname/readme.md "$pkgdir"/usr/share/doc/"$_pkgname"/readme.md
  # shorcuts
  install -Dm755 freeablo.desktop "$pkgdir"/usr/share/applications/freeablo.desktop
  #### I(Eden) Created this Icon. It is LICENSED(GPL).
  install -Dm755 freeablo_icon.svg "$pkgdir"/usr/share/icons/freeablo_icon.svg
  install -Dm755 freeablo.sh "$pkgdir"/usr/bin/freeablo
  #### NOTE: DIABLO 1, is ABANDONWARE. Please NOTE: This Game is still licensed by Blizzard. Please ...
  #### Support future Releases.
  
  #install -m755 "$srcdir"/DIABDAT.MPQ "$pkgdir"/usr/share/freeablo/resources/DIABDAT.MPQ
  #install -m755 "$srcdir"/Diablo.exe "$pkgdir"/usr/share/freeablo/resources/Diablo.exe
}
