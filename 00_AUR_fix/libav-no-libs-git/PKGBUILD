# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix

pkgname=libav-no-libs-git
pkgver=13_dev0.r1648.gc4642788e
pkgrel=1
pkgdesc='Open source audio and video processing tools'
arch=('i686' 'x86_64')
url='https://libav.org/'
license=('LGPL')
depends=('zlib' 'bzip2' 'openssl')
makedepends=('git' 'yasm')
conflicts=('libav' 'libav-no-libs' 'libav-no-libs-git' 'libav-git-no-libs')
provides=('libav-no-libs')
source=($pkgname::git://git.libav.org/libav.git)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$pkgname"

  ./configure \
    --prefix=/usr \
    --enable-openssl \
    --enable-nonfree

  make
}

package() {
  cd "$srcdir/$pkgname"

  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{include,lib}
}
