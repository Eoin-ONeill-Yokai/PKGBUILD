# Maintainer:  Νικόλαος Κυριάκος Φυτίλης <n-fit@live.com>
# Contributor: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=mono-upnp-git
pkgver=0.1.2.r4.g09214f9
pkgrel=1
pkgdesc="UPNP binding for Mono/.NET"
arch=(i686 x86_64)
url="https://github.com/mono/mono-upnp"
license=(MIT)
depends=(gtk-sharp-2 mono-addins taglib-sharp-git)
makedepends=(git)
conflicts=(mono-upnp)
provides=(mono-upnp)
source=("$pkgname::git+https://github.com/mono/mono-upnp.git"
        https://github.com/DynamicDevices/mono-upnp/commit/95780e8ec440c3714a6fcf167cf7cd1d0f55d94a.diff)
md5sums=('SKIP'
         'b2f9bfb0a6d9f64fa8754129db40c44b')

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgname"
  patch -p1 -i $srcdir/95780e8ec440c3714a6fcf167cf7cd1d0f55d94a.diff
  ./autogen.sh --prefix=/usr --disable-tests
}

build() {
  cd "$pkgname"
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
