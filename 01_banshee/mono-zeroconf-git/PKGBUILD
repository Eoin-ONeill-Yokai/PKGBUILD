# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=mono-zeroconf-git
pkgver=0.9.0.r9.g6132666
pkgrel=1
pkgdesc="A cross platform Zero Configuration Networking library for Mono and .NET."
url="http://www.mono-project.com/Mono.Zeroconf"
license=(GPL)
arch=(any)
depends=(mono avahi dbus-sharp)
makedepends=(git)
provides=(mono-zeroconf)
conflicts=(mono-zeroconf)
source=("$pkgname::git+https://github.com/arfbtwn/Mono.Zeroconf#branch=feature/dbus-sharp")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/^Mono.Zeroconf.//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgname"
  ./autogen.sh
}

build() {
  cd "$pkgname"
    ./configure --prefix=/usr --disable-docs --disable-mdnsresponder
    make hicolordir=/usr/share/icons/hicolor GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1
}

package() {
  cd "$pkgname"
  make DESTDIR=${pkgdir} install
}
