# Maintainer: Nikos Fytilis <n-fit@live.com>
# Repo Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Carlos Ruiz <cailovirtual@gmail.com>

pkgname=taglib-sharp-git
pkgver=2.1.0.0.r220.gfe2cc47
pkgrel=1
pkgdesc="Library for reading and writing metadata in media files for Mono - source from git for working opus files"
arch=(i686 x86_64)
url="https://github.com/mono/taglib-sharp"
license=(LGPL2.1)
depends=(mono)
makedepends=(git)
conflicts=(taglib-sharp)
provides=(taglib-sharp)
source=("$pkgname::git+https://github.com/mono/taglib-sharp.git"
        sign.patch)
md5sums=('SKIP'
         'fb6bb9fc76b88a449fb1466569194da0')

pkgver() {
  cd "$pkgname"
  git describe --long | sed 's/^taglib-sharp-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgname"
  patch src/Makefile.am "$srcdir/sign.patch"
  ./autogen.sh --prefix=/usr --disable-docs
}

build() {
  cd "$pkgname"
  make
}

package() {
  cd "$pkgname"
  make DESTDIR=${pkgdir} install
}
