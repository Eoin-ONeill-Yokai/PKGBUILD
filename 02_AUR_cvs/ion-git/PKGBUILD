# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>

pkgname=ion-git
pkgver=1.0.5.r724.ged1250c
pkgrel=1
pkgdesc="The Ion Shell. Compatible with Redox and Linux. (Git version)"
arch=("x86_64" "i686")
url="https://github.com/redox-os/ion"
license=(MIT)
makedepends=(git mdbook rust)
provides=(ion)
conflicts=(ion)
install=${pkgname}.install
source=("${pkgname}::git+https://github.com/redox-os/ion.git"
        "MIT::https://raw.githubusercontent.com/redox-os/ion/master/LICENSE")
sha512sums=('SKIP'
            '373cd0dd73c29aee38cb3bb8046345f19776c8047342fff1a055831d027cba6348a9ba23eb389883837048fe32d31461f66e266bed4a18849dd13ffd67f32ae8')

pkgver() {
  cd "${pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/${pkgname}"
  cargo build --release

  cd "$srcdir/${pkgname}/manual"
  mdbook build
}

check() {
  cd "$srcdir/${pkgname}"
  cargo test
}

package() {
  cd "$srcdir/${pkgname}"
  install -Dm755 target/release/ion $pkgdir/usr/bin/ion

  install -Dm644 "${srcdir}/MIT" "$pkgdir/usr/share/licenses/${pkgname}/MIT"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r "${srcdir}/${pkgname}/manual/book" "${pkgdir}/usr/share/doc/${pkgname}"
}
