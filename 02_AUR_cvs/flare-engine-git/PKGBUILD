# Contributor: dorkster <jajdorkster@gmail.com>

pkgname=flare-engine-git
pkgver=1.06.r195.gd0240d4b
pkgrel=1
pkgdesc="Free/Libre Action Roleplaying Engine - Git version"
url="http://www.flarerpg.org/"
license=(GPL3)
arch=(i686 x86_64)
makedepends=(git cmake)
depends=(sdl2_image sdl2_mixer sdl2_ttf hicolor-icon-theme python)
provides=(flare-engine)
conflicts=(flare-engine)
source=("git+https://github.com/clintbellanger/flare-engine")
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname/-git/}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/${pkgname/-git/}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DBINDIR=bin -DDATADIR=share/flare
  make
}

package() {
  cd "$srcdir/${pkgname/-git/}"
  make install DESTDIR=$pkgdir
}
