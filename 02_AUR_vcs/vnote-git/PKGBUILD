# Maintainer: erk <v at erk dot io>

pkgname=vnote-git
pkgver=2.2.r3.ge3b9d06
pkgrel=1
pkgdesc="A Vim-inspired note-taking application, especially for Markdown."
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url='https://tamlok.github.io/vnote/'
license=(MIT)
depends=(qt5-base qt5-webengine qt5-svg)
makedepends=(git)
source=("git+https://github.com/tamlok/vnote.git"
        "git+https://github.com/chjj/marked.git"
        "git+https://github.com/tamlok/hoedown.git")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')
_pkgname=vnote

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/$_pkgname"

  git submodule init
  git config 'submodule.src/utils/marked.url' "${srcdir}/marked"
  git config 'submodule.hoedown.url' "${srcdir}/hoedown"
  git submodule update

  install -d build
}

build() {
  cd "$srcdir/$_pkgname/build"
  qmake-qt5 ../VNote.pro
  make
}

package() {
  cd "$srcdir/$_pkgname/build"
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 "$srcdir/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}
