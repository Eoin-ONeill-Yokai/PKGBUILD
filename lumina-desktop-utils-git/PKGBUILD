# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org>
# Maintainer: E5ten

pkgbase=lumina-desktop-utils-git
pkgname=(lumina-icon-theme-git lumina-archiver-git lumina-calculator-git lumina-fileinfo-git lumina-fm-git lumina-mediaplayer-git lumina-pdf-git 
         lumina-screenshot-git lumina-textedit-git lumina-notify-git lumina-xdg-entry-git lumina-open-git) #lumina-screencast-git lumina-terminal-git
pkgver=1.4.0.r433.gcd407705
pkgrel=1
pkgdesc="Desktop applications from Lumina project"
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="https://github.com/trueos/lumina"
license=(BSD)
depends=(qt5-base)
makedepends=(qt5-svg qt5-tools qt5-x11extras qt5-multimedia poppler-qt5 git)
conflicts=(lumina-desktop lumina-desktop-git)
source=("git+https://github.com/lumina-desktop/lumina")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/lumina"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

_lumina-build() {
    cd "${srcdir}/lumina/src-qt5/${_location}/${pkgname%-git}"
    qmake QMAKE_CFLAGS_ISYSTEM= PREFIX="/usr" LIBPREFIX=/usr/lib QT5LIBDIR=/usr/lib/qt CONFIG+=WITH_I18N L_MANDIR=/usr/share/man L_ETCDIR="/etc"
    make
    make INSTALL_ROOT="${pkgdir}" install
    install -D "${srcdir}/lumina/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_lumina-icon-theme-git() {
  pkgname=lumina-icon-theme-git
  pkgdesc="Icon theme for Lumina desktop applications"
  arch=(any)
  depends=()
  provides=(lumina-icon-theme)
  conflicts=(lumina-icon-theme)
  options=(!strip)

    cd "${srcdir}/lumina/icon-theme"
    qmake QMAKE_CFLAGS_ISYSTEM= PREFIX="/usr" LIBPREFIX=/usr/lib QT5LIBDIR=/usr/lib/qt CONFIG+=WITH_I18N L_MANDIR=/usr/share/man L_ETCDIR="/etc"
    make
    make INSTALL_ROOT="${pkgdir}" install
    install -D "${srcdir}/lumina/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_lumina-archiver-git() {
  pkgname=lumina-archiver-git
  pkgdesc="Archivier for the lumina desktop environment"
  depends+=(lumina-icon-theme)
  provides=(lumina-archiver)
  conflicts=(lumina-archiver)

  _location=desktop-utils
  _lumina-build
}

package_lumina-calculator-git() {
  pkgname=lumina-calculator-git
  pkgdesc="Calculator for the lumina desktop environment"
  depends+=(lumina-icon-theme)
  provides=(lumina-calculator)
  conflicts=(lumina-calculator)

  _location=desktop-utils
  _lumina-build
}

package_lumina-fileinfo-git() {
  pkgname=lumina-fileinfo-git
  pkgdesc="File property manager for the lumina desktop environment"
  depends+=(qt5-multimedia lumina-icon-theme)
  provides=(lumina-fileinfo)
  conflicts=(lumina-fileinfo)

  _location=desktop-utils
  _lumina-build
}

package_lumina-fm-git() {
  pkgname=lumina-fm-git
  pkgdesc="File manager for the lumina desktop environment"
  depends+=(qt5-x11extras qt5-multimedia lumina-icon-theme lumina-open lumina-fileinfo)
  provides=(lumina-fm)
  conflicts=(lumina-fm)

  _location=desktop-utils
  _lumina-build
}

package_lumina-mediaplayer-git() {
  pkgname=lumina-mediaplayer-git
  pkgdesc="Mediaplayer for the lumina desktop environment"
  depends+=(qt5-x11extras qt5-multimedia lumina-icon-theme)
  provides=(lumina-mediaplayer)
  conflicts=(lumina-mediaplayer)

  _location=desktop-utils
  _lumina-build
}

package_lumina-pdf-git() {
  pkgname=lumina-pdf-git
  pkgdesc="PDF viewver for the lumina desktop environment"
  depends+=(poppler-qt5 lumina-icon-theme)
  provides=(lumina-pdf)
  conflicts=(lumina-pdf)

  _location=desktop-utils
  _lumina-build
}

package_lumina-screenshot-git() {
  pkgname=lumina-screenshot-git
  pkgdesc="Screenshot utility for the lumina desktop environment"
  depends+=(qt5-x11extras lumina-icon-theme)
  provides=(lumina-screenshot)
  conflicts=(lumina-screenshot)

  _location=desktop-utils
  _lumina-build
}

package_lumina-textedit-git() {
  pkgname=lumina-textedit-git
  pkgdesc="Text editor for the lumina desktop environment"
  depends+=(lumina-icon-theme)
  provides=(lumina-textedit)
  conflicts=(lumina-textedit)

  _location=desktop-utils
  _lumina-build
}

package_lumina-notify-git() {
  pkgname=lumina-notify-git
  pkgdesc=""
  provides=(lumina-notify)
  conflicts=(lumina-notify)

  _location=desktop-utils
  _lumina-build
}

package_lumina-xdg-entry-git() {
  pkgname=lumina-xdg-entry-git
  pkgdesc=""
  provides=(lumina-xdg-entry)
  conflicts=(lumina-xdg-entry)

  _location=desktop-utils
  _lumina-build
}

package_lumina-screencast-git() {
  pkgname=lumina-screencast-git
  pkgdesc="(experimental)"
  depends+=(lumina-icon-theme)
  provides=(lumina-screencast)
  conflicts=(lumina-screencast)

  _location=experimental
  _lumina-build
}

package_lumina-terminal-git() {
  pkgname=lumina-terminal-git
  pkgdesc="(experimental)"
  depends+=(lumina-icon-theme)
  provides=(lumina-terminal)
  conflicts=(lumina-terminal)

  _location=experimental
  _lumina-build
}

package_lumina-open-git() {
  pkgname=lumina-open-git
  pkgdesc="Application launcher for the lumina desktop environment"
  provides=(lumina-open)
  conflicts=(lumina-open)

  _location=core
  _lumina-build
}

