# Maintainer: Que Quotion <quequotion@bugmenot.com>
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=brasero-git
pkgver=3.12.2+45+g6c45f9e7
pkgrel=1
pkgdesc="CD/DVD mastering tool"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Apps/Brasero"
license=(GPL)
options=(!emptydirs)
conflicts=('brasero')
provides=('brasero')
depends=(gtk3 gst-plugins-good totem-plparser cdrkit cdrdao shared-mime-info libcanberra
         dvd+rw-tools dconf libsm tracker libnotify gvfs)
makedepends=(gtk-doc yelp-tools gnome-common intltool libburn libisofs libnautilus-extension
             gobject-introspection gconf itstool docbook-xsl)
optdepends=('libburn: alternative back-end'
            'libisofs: libburn back-end'
            'dvdauthor: video project'
            'vcdimager: video project')
replaces=(nautilus-cd-burner)
groups=(gnome-extra)
source=("git+https://git.gnome.org/browse/brasero.git")
sha256sums=('SKIP')

pkgver() {
  cd brasero
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd brasero
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-caches --disable-schemas-compile --enable-compile-warnings=minimum
  make
}

package() {
  cd brasero
  make DESTDIR=$pkgdir install
}

