# Maintainer: Jason Mooradian <cowmix@gmail.com>
# Contributor: Jeff Keyte <jeff@keyte.me>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Daniel Wallace <daniel.wallace@gatech.edu>

pkgname=openpht
_pkgver=1.8.0.148-573b6d73
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="OpenPHT is a community driven fork of Plex Home Theater"
arch=(i686 x86_64)
url="https://github.com/RasPlex/OpenPHT/"
license=(GPL2)
provides=(plex-home-theater)
conflicts=(plex-home-theater)
depends=(ffmpeg-openpht alsa-lib avahi boost-libs expat freetype2 fribidi
         gcc-libs glew glu libcdio libgl libjpeg libmad
         libmicrohttpd libmpeg2 libpng libpulse libsamplerate llvm-libs
         libssh libtiff libusb-compat libva libvdpau libx11
         libxext libxrandr lzo pcre python2 sdl sdl_image
         sdl_mixer sqlite smbclient taglib tinyxml yajl)
makedepends=(boost cmake doxygen libcec nasm swig)
optdepends=('libcec: Pulse-Eight USB-CEC adapter support'
            'pulseaudio: PulseAudio support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/RasPlex/OpenPHT/archive/v${_pkgver}.tar.gz"
        plexhometheater.sh
        fribidi.patch
        plexhometheater.desktop)
sha256sums=('48f03cd1b5aa8b8d9e0b58e080ed3293d628cbf39c75f227cfb2569cb519cfe3'
            '8a2d9ac55bc4e05520a9dc5893bb19c5e0e28991dbcdd9b4af5feaec20fc4f2d'
            '966edfa84fa09cf9c54281dff2adfd6fad41309b17893511e7cf01dc85def1ea'
            '354429ce7dcafaa8b619b000000f4f4f8fc0a1545b9656f6beb9f85ebb8f4e41')
prepare() {
  cd "OpenPHT-${_pkgver}"

  patch -Np1 <../fribidi.patch
  install -d build
}

build() {
  cd "OpenPHT-${_pkgver}/build"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DUSE_INTERNAL_FFMPEG=OFF \
    -DENABLE_AUTOUPDATE=OFF \
    -DENABLE_SHAIRPLAY=OFF \
    -DENABLE_SHAIRPORT=OFF
  make
}

package() {
  cd "OpenPHT-${_pkgver}/build"

  make DESTDIR="${pkgdir}" install
  install -dm 755 "${pkgdir}"/usr/{lib/plexhometheater,share/{applications,pixmaps}}
  mv "${pkgdir}"/usr/bin/openpht "${pkgdir}"/usr/lib/plexhometheater/
  mv "${pkgdir}"/usr/share/openpht "${pkgdir}"/usr/share/plexhometheater
  rm -rf "${pkgdir}"/usr/bin/*.so
  install -m 755 "${srcdir}"/plexhometheater.sh "${pkgdir}"/usr/bin/
  install -m 644 "${srcdir}"/plexhometheater.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 ../plex/Resources/plex-icon-256.png "${pkgdir}"/usr/share/pixmaps/plexhometheater.png
}
