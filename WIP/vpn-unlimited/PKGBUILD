# Maintainer: Troy Engel <troyengel+arch@gmail.com>

pkgname=vpn-unlimited
pkgver=2.8.0
pkgrel=7
pkgdesc="VPN client for the VPN Unlimited service"
arch=('x86_64')
url="https://www.vpnunlimitedapp.com/downloadlinux"
license=('custom')
options=('emptydirs')
depends=('qt4' 'libssh2' 'libcurl-compat' 'libjson')
source=("http://apt.simplexsolutionsinc.com/repository/pool/main/v/vpn-unlimited/${pkgname}_${pkgver}_amd64.deb"
        "vpn-unlimited-daemon.service")
md5sums=('e9a1cae5c4e65ad87ee7120dc5b3457c'
         '7c3269456d352df8d676c722058b5067')

package() {
  # Unpack the vendor package
  cd "${srcdir}"
  tar -zxf data.tar.gz
  cp -dpr "${srcdir}/usr" "${pkgdir}"
  mv ${pkgdir}/usr/sbin ${pkgdir}/usr/bin
  install -Dm0644 "${pkgdir}/usr/share/doc/vpn-unlimited/copyright" \
    "${pkgdir}/usr/share/licenses/${pkgname}/copyright"

  ln -s /usr/lib/libjsoncpp.so ${pkgdir}/usr/lib/libjsoncpp.so.0


#  ln -s /usr/lib/libsfml-window.so.2.4 "${pkgdir}"/usr/lib/libsfml-window.so.2.3

  # Create a custom script that forces the older libcurl
  echo '#!/bin/sh' > "${pkgdir}/usr/bin/vpnu-arch"
  echo 'LD_PRELOAD=libcurl.so.3 vpn-unlimited' >> "${pkgdir}/usr/bin/vpnu-arch"
  chmod 0755 "${pkgdir}/usr/bin/vpnu-arch"

  # Alter the desktop file to launch our script instead
  sed -i 's/Exec=vpn-unlimited/Exec=vpnu-arch/g' "${pkgdir}/usr/share/applications/vpn-unlimited.desktop"

  # Add our custom systemd unit
  install -Dm0644 "${srcdir}/vpn-unlimited-daemon.service" "${pkgdir}/usr/lib/systemd/system/vpn-unlimited-daemon.service"

  # Save this original file
  cp -p "${srcdir}/etc/init.d/vpn-unlimited-daemon" "${pkgdir}/usr/share/doc/vpn-unlimited/"
}

