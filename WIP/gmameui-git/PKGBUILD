# Contributor: Slash <demodevil5 [AT] yahoo [DOT] com>

pkgname=gmameui-git
pkgver=r279.ea144d7
pkgrel=1
pkgdesc='SDLMAME/XMAME frontend designed to look like MAME32 (fork of GXMAME)'
arch=('i686' 'x86_64')
url='https://github.com/sergiomb2/gmameui/'
license=('GPL')
depends=('libzip' 'libarchive' 'vte' 'gtkimageview')
makedepends=('intltool' 'gettext')
source=("${pkgname}::git+https://github.com/sergiomb2/gmameui.git")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$pkgname"

    # archive_read_data_into_buffer() is deprecated in libarchive
    /bin/sed -i 's/archive_read_data_into_buffer/archive_read_data/' src/gmameui-zip-utils.c

    # replace individual glib headers with glib.h
    /bin/sed -i '34,36d' src/gmameui.c
    /bin/sed -i '33 a\#include <glib.h>' src/gmameui.c

  autoreconf -fiv
  intltoolize

    #sh autogen.sh 
    ./configure --prefix=/usr --disable-libgnome --enable-joystick
    make
}

package() {
    cd "$srcdir/$pkgname"

    make DESTDIR="$pkgdir" install
}
