# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix
# Contributor: Slash <demodevil5 [AT] yahoo [DOT] com>

pkgname=gmameui-git
pkgver=r279.ea144d7
pkgrel=1
pkgdesc='SDLMAME/XMAME frontend designed to look like MAME32 (fork of GXMAME)'
arch=('i686' 'x86_64')
url='https://github.com/sergiomb2/gmameui/'
license=('GPL')
depends=('libzip' 'libarchive' 'vte-legacy' 'gtkimageview')
makedepends=('intltool' 'gettext' 'git')
source=("${pkgname}::git+https://github.com/sergiomb2/gmameui.git")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$pkgname"

  autoreconf -fiv
  intltoolize

    ./configure --prefix=/usr --disable-libgnome --enable-joystick
    make
}

package() {
    cd "$srcdir/$pkgname"

    make DESTDIR="$pkgdir" install
}
