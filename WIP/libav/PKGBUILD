pkgname=libav
pkgver=12.3
pkgrel=1
pkgdesc="Open source audio and video processing tools forked from ffmpeg"
arch=('i686' 'x86_64')
url="http://www.libav.org/"
license=('LGPL')
depends=('alsa-lib' 'bzip2' 'libtheora' 'libvpx' 'x264' 'xvidcore' 'faac' 'lame' 'opus' 'libvorbis' 'freetype2' 'gnutls' 'openssl' 'rtmpdump')
makedepends=('yasm')
provides=('libav' 'ffmpeg')
conflicts=('libav' 'ffmpeg' 'libav-no-libs' 'libav-no-libs-git' 'libav-git-no-libs')
source=(http://libav.org/releases/$pkgname-$pkgver.tar.xz)
md5sums=('753ec26481b0582eb737383bd8a350a5')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-static \
    --disable-debug \
    --enable-gpl \
    --enable-nonfree \
    --enable-libtheora  \
    --enable-libvpx \
    --enable-libx264 \
    --enable-libxvid \
    --enable-libfaac \
    --enable-libmp3lame \
    --enable-libopus \
    --enable-libvorbis \
    --enable-libfreetype \
    --enable-gnutls \
    --enable-librtmp
  make
  make doc/av{play,probe,conv}.1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install install-man
}

