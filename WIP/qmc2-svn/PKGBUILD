# Maintainer: Fabio Loli <loli_fabio@protonmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>
# https://github.com/FabioLolix

pkgname=qmc2-svn
pkgver=0.193.8265
pkgrel=1
pkgdesc="Qt 5 based UNIX MAME frontend supporting SDLMAME"
url="http://qmc2.batcom-it.net/"
license=("GPL")
arch=('i686' 'x86_64')
depends=('qt5-webkit' 'qt5-svg' 'qt5-multimedia' 'phonon-qt5' 'sdl2')
makedepends=('subversion' 'rsync' 'mesa')
provides=('qmc2')
conflicts=('qmc2' 'qmc2-svn')
source=("qmc2::svn://svn.code.sf.net/p/qmc2/code/trunk")
sha256sums=('SKIP')

pkgver() {
  cd qmc2
  _ver="$(cat Makefile | grep -m2 -e VERSION_MAJOR -e VERSION_MINOR | grep -o "[[:digit:]]*" | paste -sd'.')"
  echo "${_ver}.$(svnversion)"
}

build() {
  cd qmc2
  export CTIME=0
  make PREFIX=/usr DATADIR=/usr/share SYSCONFDIR=/etc QTDIR=/usr QMAKE=qmake-qt5
}

package() {
  cd qmc2
  make PREFIX=/usr DATADIR=/usr/share SYSCONFDIR=/etc QTDIR=/usr QMAKE=qmake-qt5 DESTDIR="$pkgdir" install
  install -d "$pkgdir"/usr/share/{applications,pixmaps}
  install -D -m644 "$srcdir/qmc2/data/img/mame.png" "$pkgdir/usr/share/pixmaps"
}
