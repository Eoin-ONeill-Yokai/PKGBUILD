# Maintainer: Michael Straube <straubem@gmx.de>

pkgname=bluemindo-svn
pkgver=r215
pkgrel=1
pkgdesc='Ergonomic and modern music player designed for audiophiles'
arch=('any')
url='http://bluemindo.codingteam.net'
license=('GPL3')
depends=('gst-python' 'python-dbus' 'python-beautifulsoup4' 'python-pytaglib' 'gtk3')
optdepends=('pulseaudio: PulseAudio support'
            'gst-plugins-good: PulseAudio support and good codecs'
            'gst-plugins-bad: additional codecs'
            'gst-plugins-ugly: additional codecs'
            'gst-libav: libav codec')
makedepends=('subversion')
provides=('bluemindo')
conflicts=('bluemindo')
source=("${pkgname}::svn+http://svn.codingteam.net/bluemindo"
        "bluemindo.patch")
sha256sums=('SKIP'
            '3f8758bd2255966af796937902e1ff5064df3f34283172eaee8b28d855fb0747')

pkgver() {
  cd ${pkgname}
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd ${srcdir}/${pkgname}/trunk

  # patch Makefile
  patch -p1 -i ../../bluemindo.patch
}

package() {
  cd ${srcdir}/${pkgname}/trunk

  make DESTDIR="$pkgdir" install
}
