# Maintainer: Chris <christopher.r.mullins g-mail>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=gdcm
pkgver=2.8.4
pkgrel=1
pkgdesc='a C++ library for DICOM medical files'
arch=(i686 x86_64)
url='http://gdcm.sourceforge.net'
license=(BSD)
makedepends=(cmake)
source=("http://sourceforge.net/projects/gdcm/files/gdcm%202.x/GDCM%20$pkgver/gdcm-$pkgver.tar.gz")
sha512sums=('b810d60b71ee91c74237c10307e553ef628a2cda0b7c8c6a753b0ff2c09964e3b8a17e1e826889f925402e3c415f6e54d38c10b0dc86123c7091e4f8e3a8c656')

prepare() {
  cd "$pkgname-$pkgver"
  install -d build
}

build() {
  cd "$pkgname-$pkgver/build"
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DGDCM_BUILD_SHARED_LIBS=ON \
    -DGDCM_BUILD_TESTING=OFF \
    -DGDCM_BUILD_DOCBOOK_MANPAGES=OFF
    make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir" install

  install -Dm644 ../Copyright.txt "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}

