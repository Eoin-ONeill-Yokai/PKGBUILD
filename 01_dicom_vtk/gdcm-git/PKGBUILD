# Maintainer: Peter Mattern <pmattern at arcor dot de>

pkgname=gdcm-git
pkgver=2.8.8.223.g55ddb98e9
pkgrel=1
pkgdesc='Open source implementation of the DICOM standard'
arch=('i686' 'x86_64')
url='http://gdcm.sourceforge.net'
license=('BSD')
depends=('poppler')
makedepends=('git' 'cmake' 'libxslt' 'dcmtk')
provides=(gdcm)
conflicts=(gdcm)
source=("git://git.code.sf.net/p/gdcm/gdcm")
sha256sums=("SKIP")

pkgver() {
  cd "$srcdir/${pkgname/-git/}"
  git describe --tags | sed 's:^v::;s:-:.:g'
}

prepare() {
  cd "$srcdir/${pkgname/-git/}"
  install -d build
}

build() {
  cd "$srcdir/${pkgname/-git/}/build"
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DGDCM_BUILD_APPLICATIONS=ON \
    -DGDCM_BUILD_SHARED_LIBS=ON \
    -DGDCM_LEGACY_REMOVE=ON \
    -DGDCMV2_0_COMPATIBILITY=OFF \
    -DGDCM_USE_SYSTEM_CHARLS=OFF \
    -DGDCM_USE_SYSTEM_EXPAT=ON \
    -DGDCM_USE_SYSTEM_JSON=OFF \
    -DGDCM_USE_SYSTEM_LIBXML2=ON \
    -DGDCM_USE_SYSTEM_LJPEG=OFF \
    -DGDCM_USE_SYSTEM_OPENJPEG=ON \
    -DGDCM_USE_SYSTEM_OPENSSL=ON \
    -DGDCM_USE_SYSTEM_POPPLER=ON \
    -DGDCM_USE_SYSTEM_SOCKETXX=OFF \
    -DGDCM_USE_SYSTEM_ZLIB=ON
  make
}

package() {
  cd "$srcdir/${pkgname/-git/}/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../Copyright.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
