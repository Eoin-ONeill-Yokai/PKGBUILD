# Maintainer: Fabio Loli <loli_fabio@protonmail.com>
# https://github.com/FabioLolix

pkgname=twin-git
_gitname="twin"
pkgver=r172.a877205
pkgrel=1
pkgdesc="A text-mode window environment"
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/twin/'
license=('GPL2' 'LGPL2.1')
depends=('gpm' 'libxpm')
provides=('twin')
conflicts=('twin' 'twin-git')
options=('!makeflags')
source=("twin-git::git+https://github.com/cosmos72/twin")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir"/$pkgname
  LDFLAGS="-lX11" ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
	--enable-tt=yes \
	--enable-tt-hw-twin=yes \
    --enable-tt-hw-twin-tunnel=yes \
    --enable-tt-hw-twin-detunnel=yes \
	--enable-tt-hw-xml=yes
  make
}

package() {
  cd "$srcdir"/$pkgname
  make DESTDIR="$pkgdir" install
}
