# Maintainer: Fabio Loli <loli_fabio@protonmail.com> -> https://github.com/FabioLolix
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>

pkgname=twin
pkgver=0.7.4
pkgrel=1
pkgdesc="A text-mode window environment"
arch=('x86_64')
url='https://github.com/cosmos72/twin'
license=('GPL2' 'LGPL2.1')
depends=('gpm' 'zlib' 'libxpm')
makedepends=('git')
options=('!makeflags')
_commit=9ec799d0ac03e90b12b8030b097d20aa61c40b5a # tag 0.7.4
source=("git+https://github.com/cosmos72/twin.git#commit=$_commit")
md5sums=('SKIP')

build() {
  cd "$srcdir"/$pkgname
  LDFLAGS="-lX11" ./configure --prefix=/usr --sbindir=/usr/bin \
    --enable-tt=yes \
    --enable-tt-hw-twin=yes \
    --enable-tt-hw-twin-tunnel=yes \
    --enable-tt-hw-twin-detunnel=yes \
    --enable-tt-hw-xml=yes
  make
}

package() {
  cd "$srcdir"/$pkgname
  make DESTDIR="$pkgdir" install
}
