# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix
# Contributor: Florian Pritz <f-p@gmx.at>

pkgname=inxi-dev-git
pkgver=r752.a15d9a93
pkgrel=1
pkgdesc="Pinxi (perl-inxi), development branch of inxi, a full featured CLI system information tool"
arch=('any')
url="http://inxi.org"
license=('GPL3')
depends=(coreutils gawk grep pciutils procps-ng sed perl)
optdepends=(
  "dmidecode: inxi -M if no sys machine data"
  "file: inxi -o unmounted file system"
  "hddtemp: inxi -Dx show hdd temp"
  "net-tools: inxi -i ip lan-deprecated"
  "iproute2: inxi -i ip lan"
  "lm_sensors: inxi -s sensors output"
  "usbutils: inxi -A usb audio;-N usb networking"
  "kmod: inxi -Ax,-Nx module version"
  "systemd-sysvcompat: inxi -I runlevel"
  "sudo: inxi -Dx hddtemp-user;-o file-user"
  "mesa-demos: inxi -G glx info"
  "xorg-xdpyinfo: inxi -G multi screen resolution"
  "xorg-xprop: inxi -S desktop data"
  "xorg-xrandr: inxi -G single screen resolution"
)
source=("${pkgname}::git+https://github.com/smxi/inxi.git#branch=inxi-perl")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "${srcdir}/${pkgname}"
  install -D -m755 pinxi "$pkgdir/usr/bin/pinxi"
  install -D -m755 pinxi.1 "$pkgdir/usr/share/man/man1/pinxi.1.gz"
}
