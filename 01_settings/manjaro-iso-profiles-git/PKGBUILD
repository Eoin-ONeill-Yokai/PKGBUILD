
pkgname=manjaro-iso-profiles-git
pkgver=r1038.ecda1bb
pkgrel=1
pkgdesc="ISO profiles for Manjaro Linux based distros, latest git commits"
arch=('any')
url="https://gitlab.manjaro.org/profiles-and-settings/iso-profiles"
license=('GPL')
provides=('manjaro-iso-profiles-base'
        'manjaro-iso-profiles-official'
        'manjaro-iso-profiles-community')
conflicts=('manjaro-iso-profiles-base'
        'manjaro-iso-profiles-official'
        'manjaro-iso-profiles-community')
source=("${pkgname}::git+https://gitlab.manjaro.org/profiles-and-settings/iso-profiles.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

_inst_dir() {
    install -d $pkgdir/usr/share/manjaro-tools/iso-profiles
    cp -a $srcdir/$pkgname/$1 $pkgdir/usr/share/manjaro-tools/iso-profiles/
}

package() {
    _inst_dir 'shared'
    cp $srcdir/$pkgname/repo_info $pkgdir/usr/share/manjaro-tools/iso-profiles/
    _inst_dir 'manjaro'
    _inst_dir 'community'
}
