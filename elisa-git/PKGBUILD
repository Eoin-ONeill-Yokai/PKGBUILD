# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>
# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=elisa-git
pkgver=0.4.0.r53.gbce759c
pkgrel=1
arch=(i686 x86_64)
pkgdesc="A simple music player for KDE aiming to provide a nice experience for its users"
url="https://community.kde.org/Elisa"
license=(GPL3 custom:"Qt Company GPL Exception 1.0")
depends=(qt5-quickcontrols qt5-quickcontrols2)
        
makedepends=(git python extra-cmake-modules kdoctools baloo kcmutils kirigami2 qt5-multimedia)
optdepends=(baloo)
provides=(elisa)
conflicts=(elisa)
source=("git://anongit.kde.org/elisa.git")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
  mkdir -p build
}

build() {
  cd "${pkgname%-git}/build"
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package(){
  cd "${pkgname%-git}/build"
  make DESTDIR="$pkgdir" install

  install -Dm644 ../LICENSE.GPL3-EXCEPT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.GPL3-EXCEPT"
}
