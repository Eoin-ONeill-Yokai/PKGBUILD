
pkgname=xf86-video-sisimedia-rkrell-git
_pkgname=xf86-video-sisimedia
pkgver=r39.85a1036
pkgrel=1
pkgdesc="X11 video driver for SiS 671/771 cards"
arch=('i686' 'x86_64')
url="https://github.com/rkrell/xf86-video-sisimedia"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=23' 'xf86dgaproto')
provide=('xf86-video-sisimedia')
conflicts=('xf86-video-sisimedia' 'xorg-server<1.19' 'X-ABI-VIDEODRV_VERSION<23' 'X-ABI-VIDEODRV_VERSION>=24')
install="${_pkgname}.install"
source=("git+https://github.com/rkrell/xf86-video-sisimedia.git"
        COPYING
        "${_pkgname}.conf")
md5sums=('SKIP'
         'cbbdd887d04deb501076c22917e2030d'
         'ea53ab81de92a5dfa155f0160b960ebb')

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_pkgname}"

  ./configure --prefix=/usr --disable-dri
  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/share/man"

  install -m755 -d "${pkgdir}/usr/share/licenses/${_pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${_pkgname}/"

  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m644 "${srcdir}/${_pkgname}.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
}
