# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Mcder3 <mcder3[at]gmail[dot]com>
# Contributor: MisterFred <mister.fred[at]free[dot]fr>

pkgname=cantata-no-vlc
_pkgname=cantata
pkgver=2.1.0
pkgrel=4
pkgdesc='Qt5 client for the music player daemon (MPD)'
arch=(i686 x86_64)
url='https://github.com/CDrummond/cantata'
license=(GPL)
depends=(qt5-svg libmtp libmusicbrainz5 taglib-extras media-player-info libcdio-paranoia udisks2 qt5-multimedia)
optdepends=('perl-uri: dynamic playlist' 'mpd: playback')
makedepends=(cmake qt5-tools ffmpeg)
provides=(cantata)
conflicts=(cantata)
source=("https://github.com/CDrummond/cantata/releases/download/v$pkgver/${_pkgname}-$pkgver.tar.bz2")
md5sums=('392bf79e50429395ce8708b69c3de1c3')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname}-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_LIBVLC=OFF \
    -DENABLE_FFMPEG=OFF \
    -DENABLE_MPG123=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
