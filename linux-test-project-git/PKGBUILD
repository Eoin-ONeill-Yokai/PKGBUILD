# Maintainer: Fabio Loli <loli_fabio@protonmail.com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=linux-test-project-git
pkgver=r130.b8c78d7
pkgrel=1
pkgdesc="A collection of tools for testing the Linux kernel and related features"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://linux-test-project.github.io/"
depends=('acl' 'bash' 'gawk' 'libcap' 'libtirpc' 'python2' 'numactl' 'libaio')
source=("git+https://github.com/linux-test-project/ltp.git")
md5sums=('SKIP')

pkgver() {
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/ltp"
  find . -type f -exec sed -i 's|^#!.*python$|#!/usr/bin/env python2|' '{}' ';'
}

build() {
  cd "$srcdir/ltp"

  make autotools
  ./configure --prefix=/usr/share --mandir=/usr/share/man --with-bash 
  make all
}

package() {
  cd "$srcdir/ltp"

  make DESTDIR="$pkgdir" install
}

